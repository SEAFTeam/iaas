entities:
  seaf.ta.reverse.general.systems:
    menu: >
      (     
          $reverse_systems_menu_generator := function($prefix, $domain, $type){(
      
              $domain_matcher := $eval('/^' & $domain & '.*/');
              $available_systems := $type = "seaf" ? (                 /*       If      */
                  $systems := $$.components.$spread()[*.type = 'service'];
                  $components := $$.components.$spread()[*.type = 'component'];
                  $links := $$."seaf.ta.reverse.general.links";
                  $distinct($links.$spread().(
                      $app_id := $.*.app_id;
                      $components[$keys() = $app_id] ? (
                          $system_id := $.**.is_component_of;
                          $systems[$keys() = $system_id];
                      ) : (
                          $systems[$keys() = $app_id];
                          )
                      )
                  );
              ) : (     /*       Else        */
                  $systems := $."kadzo.v2023.systems"  ~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
                  $tech_params := $."kadzo.v2023.tech_params" ~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
                  $config := "seaf.ta.reverse.general".config;
                  $reverse_entities := $config.entities.**.model;
                  $tags := $config.entities.**.tags.*;
                  $tp := $reverse_entities.(
                      $lookup($$, $) ~> $each(function($v, $k){$domain_matcher($k) and $v.*.tags ? $v.*.tags.*.$split(", ")}) 
                  ).($append($tp, $.$split(", "))) ~> $distinct();
                  $tp.(
                      $lookup($tech_params, $)@$s.({$s.system: $lookup($systems, $s.system)})
                  ) ~> $distinct();
              );
              [$available_systems.({
                  "domain": $domain,
                  "location": $prefix & "/Системы/" & $.*.title,
                  "link": "entities/seaf.ta.reverse.general.systems/systems_ta?id=" & $keys()[0]
              })]
          )};
          sber.kadzo ? (
              sber.kadzo.enterprise_mode ?
              sber.domains.$spread().( 
                  $domain := $keys(); 
                  $title := $lookup($$.companies, $domain) ? $lookup($$.companies, $domain).title : $.*.title;
                  $prefix := "ДЗО/" & $title & "/Реверс";
                  $reverse_systems_menu_generator($prefix, $domain, "kadzo");
              ) : (
                    $not($exists(sber.kadzo.enterprise_mode)) and (sber.kadzo.menu.architecture.enabled = true or $not($exists(sber.kadzo.menu.architecture.enabled))) ? (
                        $menu_root := ($exists(sber.kadzo.menu.architecture.roots) and $count(sber.kadzo.menu.architecture.roots) > 0 and $not(sber.kadzo.menu.architecture.roots = null))  ? sber.kadzo.menu.architecture.roots :
                                        $exists(sber.kadzo.menu_root) ? sber.kadzo.menu_root : "Документы/Сбер/КА ДЗО/2023";
                    $domain := sber.domain;
                    $prefix := $menu_root & "/Реверс";
                    $reverse_systems_menu_generator($prefix, $domain, "kadzo");
                  )
              ) 
          ) : (
              $config := "seaf.ta.reverse.general".config;
              $prefix := $config.arch_menu;
              $domain := "";
              $reverse_systems_menu_generator($prefix, $domain, "seaf");
          );
      )