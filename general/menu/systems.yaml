entities:
  seaf.ta.reverse.general.systems:
    menu: >
      (   
          $menu := function($domain, $prefix, $available_systems, $entity)
              {[$available_systems.({
                  "domain": $domain,
                  "location": $prefix & "/Системы/" & $.*.title,
                  "link": "entities/" & $entity & "/systems_ta?id=" & $keys()[0] & "&domain=" & $domain
              })]};
          $reverse_systems_menu_generator := function($prefix, $domain, $type){(
      
              $domain_matcher := $eval('/^' & $domain & '.*/');      
              $type = "seaf" ? (                 /*       If      */
                  $entity := "seaf.ta.reverse.general.systems";
      
                  $systems := $$.components.$spread()[*.type = 'service'];
                  $components := $$.components.$spread()[*.type = 'component'];
                  $links := $$."seaf.ta.reverse.general.links";
                  $available_systems := $distinct($links.$spread().(
                      $app_id := $.*.app_id;
                      $components[$keys() = $app_id] ? (
                          $system_id := $.**.is_component_of;
                          $systems[$keys() = $system_id];
                      ) : (
                          $systems[$keys() = $app_id];
                          )
                      )
                  );
                  $menu($domain, $prefix, $available_systems, $entity)
              ) : (     /*       Else        */   
                  $entity := "seaf.ta.reverse.general.systems_kadzo";
                  $systems := $."kadzo.v2023.systems" ~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
                  $kb_systems := $."kadzo.v2023.kb_systems" ~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
                  $tech_params := $."kadzo.v2023.tech_params" ~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
                  $tech_services := $."kadzo.v2023.tech_services" ~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
                  $config := "seaf.ta.reverse.general".config;
                  $reverse_entities := $config.entities.**.model;
                  $tags := $config.entities.$spread()[$keys() = $domain].**.tags.*;
                  $tags_tp := $config.entities.$spread()[$keys() = $domain].**.tags.tech_params;
                  $tags_tech_services := $config.entities.$spread()[$keys() = $domain].**.tags.tech_services;
                  $tags_systems := $config.entities.$spread()[$keys() = $domain].**.tags.systems;
                  $tp := $reverse_entities.(
                      $lookup($$, $) ~> $each(function($v, $k){$domain_matcher($k) and $v.**.tags[$keys() in $tags] ? $v.**.tags[$keys() in $tags].*.$split(", ")}) 
                  ).($append($tp, $.$split(", "))) ~> $distinct();
                  $log($domain, 'domain');
                  $log($tags, 'tags');
                  $log($tp, 'tp');
                  $direct_systems := $tp.(
                      $id := $;
                      $any_sys := $append($systems, $kb_systems);
                      $log($any_sys, 'anysys');
                      $any_sys[$keys() = $id]
                  ) ~> $distinct();
                  $tech_params := $tp.(
                      $lookup($tech_params, $)@$s.({$s.system: $lookup($systems, $s.system)})
                  ) ~> $distinct();
                  $tech_services := $tp.(
                      $id := $;
                      $lookup($tech_services, $).({$id: $})
                  ) ~> $distinct();
                  $available_systems := $append($append($tech_params, $direct_systems), $tech_services);
                  $menu($domain, $prefix, $available_systems, $entity)
              );
          )};
          sber.kadzo ? (
              sber.kadzo.enterprise_mode ?
              sber.domains.$spread().( 
                  $domain := $keys(); 
                  $title := $lookup($$.companies, $domain) ? $lookup($$.companies, $domain).title : $.*.title;
                  $prefix := "ДЗО/" & $title & "/Реверс";
                  $reverse_systems_menu_generator($prefix, $domain, "kadzo");
              ) : (
                    $not($exists(sber.kadzo.enterprise_mode)) and (sber.kadzo.menu.architecture.enabled = true or $not($exists(sber.kadzo.menu.architecture.enabled))) ? (
                        $menu_root := ($exists(sber.kadzo.menu.architecture.roots) and $count(sber.kadzo.menu.architecture.roots) > 0 and $not(sber.kadzo.menu.architecture.roots = null))  ? sber.kadzo.menu.architecture.roots :
                                        $exists(sber.kadzo.menu_root) ? sber.kadzo.menu_root : "Документы/Сбер/КА ДЗО/2023";
                    $domain := sber.domain;
                    $prefix := $menu_root & "/Реверс";
                    $reverse_systems_menu_generator($prefix, $domain, "kadzo");
                  )
              ) 
          ) : (
              $config := "seaf.ta.reverse.general".config;
              $prefix := $config.arch_menu;
              $domain := "";
              $reverse_systems_menu_generator($prefix, $domain, "seaf");
          );
      )