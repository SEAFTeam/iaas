entities:
    seaf.ta.reverse.k8s.namespace:
        presentations:
            get_namespace:
                type: markdown
                template: templates/namespace.md
                source: >
                    (
                        $namespace      := $."seaf.ta.reverse.k8s.namespace".$spread()[$.*.id = $params.id];
                        {                        
                            "id"            : $params.id,
                            "name"          : $namespace.*.name
                        }
                    )
            namespace_uml:
                type: plantuml
                template: templates/namespace.puml
                source: >
                    (
                        $nsid       := "seaf.ta.reverse.k8s.namespace." & $params.id;
                        $namespace  := $lookup($."seaf.ta.reverse.k8s.namespace", $nsid);
                    
                        $namespace_services     := $."seaf.ta.components.k8s_service".*[namespace = $nsid];
                        $namespace_deployments  := $."seaf.ta.components.k8s_deployment".*[namespace = $nsid];
                        
                        /* определение связей между сервисами и деплойментами */
                        $namespace_links     := $namespace_services.(
                                $service := $;
                                $selector := $service.selector;
                                $selectorLabels := $keys($service.selector);
                                $deployments := $filter($namespace_deployments, function($deployment) {
                                    (
                                        $labels := $deployment.labels;
                                        $mismatches := $filter($selectorLabels, function($key) {
                                            (
                                                $depLabel := $lookup($labels, $key);
                                                $selLabel := $lookup($selector, $key); 
                                                
                                                /*
                                                $log($key, "$key");
                                                $log($key in $labels, "$key in $labels");
                                                $not($key in $labels)
                                                  or 
                                                $lookup($selector, $key) != $lookup($labels, $key)
                                                */
                                                $not ($depLabel = $selLabel);
                                            )
                                        });
                                        
                                        $not($count($mismatches) > 0); 
                                    )
                                });
                                
                                {
                                    "service" : $service,
                                    "deployments" : $deployments
                                }       
                        );
                    
                        /*
                        $log($namespace_links, "links");
                        $log($nsid, "nsid");
                        $log($namespace, "namespace");
                        */
                    
                        {
                            "namespace" : $namespace,
                            "services"  : $namespace_services,
                            "deployments" : $namespace_deployments,
                            "links" : $namespace_links
                        }
                    )
            namespace_schema:
                type: smartants
                source: >
                    (
                        /* загрузка функций-обработчиков */
                        $iconify    := $eval($.functions."seaf.ta.reverse.k8s".iconify);
                        $nsid       := "seaf.ta.reverse.k8s.namespace." & $params.id;
                        $namespace  := $."seaf.ta.reverse.k8s.namespace".$spread()[$keys()[0] = $params.id];
                        
                        $namespace_services     := $."seaf.ta.components.k8s_service".*[namespace = $nsid];
                        $namespace_deployments  := $."seaf.ta.components.k8s_deployment".*[namespace = $nsid];

                        /* инициализация списка объектов диаграммы данными кластера */
                        $nodes  := {
                            $namespace.*.name : {
                                "title" : "",
                                "symbol"    : "namespace-" & $nsid & "-icon",
                                "metasymbol" : {
                                    "lines" : [
                                        {
                                            "text" : $namespace.*.name
                                        }
                                    ]
                                }
                            }
                        };
                    
                        $items := $namespace_services.(
                            {
                                $namespace.*.name & "." & $.name : {
                                    "symbol" : "svc-frame-" & $.id & "-icon",
                                    "metasymbol" : {
                                        "lines" : [
                                            {
                                                "text": "123"
                                            }   
                                        ]   
                                    }
                                }
                            }       
                        );
                    
                        $nodes := $nodes ~> | $ | $merge($items) |;
                    
                        $items := $namespace_services.(
                            {
                                $namespace.*.name & "." & $.name  & ".content" : {
                                    "title"  : $.name,
                                    "symbol" : "svc-" & $.id & "-icon",
                                    "icon"   : "k8sservice",
                                    "hideTitle" : true,
                                    "link" : '/entities/seaf.ta.components.k8s_service/get_service?id=' & $.id
                                }
                            }       
                        );
                    
                        $nodes := $nodes ~> | $ | $merge($items) |;
                    
                        $items := $namespace_deployments.(
                            {
                                $namespace.*.name & "." & $.name : {
                                    "title"  : $.name,
                                    "symbol" : "dpmt-" & $.id & "-icon",
                                    "icon"   : "k8sdeployment",
                                    "hideTitle" : true,
                                    "link" : '/entities/seaf.ta.components.k8s_service/get_service?id=' & $.id
                                }
                            }       
                        );
                                        
                        $nodes := $nodes ~> | $ | $merge($items) |;
                    
                    
                        /* $log($namespace_services, "services for " & $nsid); */
                        $symbols := $merge($iconify($nodes));
                    
                        {
                            "nodes"     : $nodes,
                            "symbols"   : $symbols,
                            "config" : {
                                "hideBoundaryTitles": true
                            }
                            /* "symbols"   : entities."reverse.k8s.icons" */
                        };
                    
                    )
            namespace_deployments:
                type: table
                headers:
                    - value: name
                      text: название
                      link: link
                source: >
                    (
                        $nsid := "seaf.ta.reverse.k8s.namespace." & $params.id;
                        $deployments := $."seaf.ta.components.k8s_deployment".$spread()[*.namespace = $nsid];
                        [$deployments.$spread().(
                            {
                                "name" : $.*.name,
                                "link" : '/entities/seaf.ta.components.k8s_deployment/get_deployment?id=' & $.*.deployment_id
                            }
                        )]

                    )
            namespace_services:
                type: table
                headers:
                    - value: name
                      text: название
                      link: link
                    - value: type
                      text: тип
                source: >
                    (
                        $nsid := "seaf.ta.reverse.k8s.namespace." & $params.id;
                        $services := $."seaf.ta.components.k8s_service".$spread()[*.namespace = $nsid]; 
                        [$services.$spread().(
                            {
                                "name"  : $.*.name,
                                "type"  : $.*.type,
                                "link" : '/entities/seaf.ta.components.k8s_service/get_service?id=' & $.*.id
                            }
                        )]

                    )