entities:
  seaf.ta.reverse.cloud_ru.enterprise.orgnets:
    menu: >
      (
          $domain := '';
          $orgnets_entity_matcher := /.*\.enterprise\.orgnets$/;
          $orgs_entity_matcher := /.*\.enterprise\.orgs$/;
          $vdcs_entity_matcher := /.*\.enterprise\.vdcs$/;
          $vdcgroups_entity_matcher := /.*\.enterprise\.vdcgroups$/;
          $dcs_entity_matcher := /.*\.services\.dc$/;
          $dcs := $$.$spread()[$dcs_entity_matcher($keys())].*;
          $vdcgroups := $$.$spread()[$vdcgroups_entity_matcher($keys())].*;
          $orgnets_entity := $$.entities.$spread()[$orgnets_entity_matcher($keys())].$keys()[0];
          $orgnets := $$.$spread()[$orgnets_entity_matcher($keys())].*;
          $orgs := $$.$spread()[$orgs_entity_matcher($keys())].*;
          $vdcs := $$.$spread()[$vdcs_entity_matcher($keys())].*;
          $config := entities."seaf.ta.reverse.general".config;
          $static := [
              {
                  'location': $config.arch_menu &  '/Cloud.ru/Enterprise/Реестры/Сети',
                  'link': 'entities/' & $orgnets_entity & '/list?domain=' & $domain
              }
          ];
          $dynamic_vdc := $orgnets.$spread()[$not($keys() in $vdcgroups.*.networks.id)].(
              $id := $keys()[0];
              $name := $replace($.*.name, '/', '\/');
              $org_id := $.*.org_id;
              $org := $orgs.$spread()[$keys() = $org_id];
              $orgname := $org.*.name;
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $vdc_id := $.*.vdc_id;
              $vdcname := $vdcs.$spread()[$keys() = $vdc_id].*.name;
              $vdc_id in $vdcgroups.*.vdcs.id ? (
                  $vdcgroups.$spread()[$vdc_id in *.vdcs.id].(
                      $vdcgname := $.*.name;
                      [
                          {
                            'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgname & '/' & $vdcname & '/Сети организации/' & $name,
                            'link': 'entities/' & $orgnets_entity & '/schema?id=' & $id
                          },
                          {
                            'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgname & '/' & $vdcname & '/Сети организации',
                            'link': 'entities/' & $orgnets_entity & '/list?vdc_id=' & $vdc_id
                          }
                      ]
                  )
              ) : (
                  [
                      {
                        'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcname & '/Сети организации/' & $name,
                        'link': 'entities/' & $orgnets_entity & '/schema?id=' & $id
                      },
                      {
                        'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcname & '/Сети организации',
                        'link': 'entities/' & $orgnets_entity & '/list?vdc_id=' & $vdc_id
                      }
                  ]
              )
          );
          $dynamic_vdcgroup := $orgnets.$spread()[$keys() in $vdcgroups.*.networks.id].(
              $id := $keys()[0];
              $name := $replace($.*.name, '/', '\/');
              $org_id := $.*.org_id;
              $org := $orgs.$spread()[$keys() = $org_id];
              $orgname := $org.*.name;
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $vdcgroup_id := $.*.vdcgroup_id;
              $vdcgroupname := $vdcgroups.$spread()[$keys() = $vdcgroup_id].*.name;
              [
                  {
                    'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgroupname & '/Сети организации/' & $name,
                    'link': 'entities/' & $orgnets_entity & '/schema_vdcgroup?id=' & $id
                  },
                  {
                    'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgroupname & '/Сети организации',
                    'link': 'entities/' & $orgnets_entity & '/list?vdcgroup_id=' & $vdcgroup_id
                  }
              ]
          );
          $dynamic := $append($dynamic_vdc, $dynamic_vdcgroup);
          $append($static, $distinct($dynamic))
      )