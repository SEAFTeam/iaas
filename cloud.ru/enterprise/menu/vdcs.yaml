entities:
  seaf.ta.reverse.cloud_ru.enterprise.vdcs:
    menu: >
      (
          $domain := '';
          $vdcs_entity_matcher := /.*\.enterprise\.vdcs$/;
          $vdcgroups_entity_matcher := /.*\.enterprise\.vdcgroups$/;
          $dcs_entity_matcher := /.*\.services\.dc$/;
          $orgs_entity_matcher := /.*\.enterprise\.orgs$/;
          $dcs := $$.$spread()[$dcs_entity_matcher($keys())].*;
          $vdcs_entity := $$.entities.$spread()[$vdcs_entity_matcher($keys())].$keys()[0];
          $vdcs := $$.$spread()[$vdcs_entity_matcher($keys())].*;
          $vdcgroups := $$.$spread()[$vdcgroups_entity_matcher($keys())].*;
          $orgs := $$.$spread()[$orgs_entity_matcher($keys())].*;
          $config := entities."seaf.ta.reverse.general".config;
          $static := [
              {
                  'location': $config.arch_menu & '/Cloud.ru/Enterprise/Реестры/VDC',
                  'link': 'entities/' & $vdcs_entity & '/list?domain=' & $domain
              }];
          $dynamic_vdc := $vdcs.$spread()[$not($keys() in $vdcgroups.*.vdcs.id)].(
              $id := $keys()[0];
              $name := $.*.name;
              $vdc := $;
              $org := $orgs.$spread()[$keys() = $vdc.*.org_id];
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $orgname := $.*.org_name;
              {
                  'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $name,
                  'link': 'entities/' & $vdcs_entity & '/card?id=' & $id
              }
          );
          $dynamic_vdcgroup := $vdcs.$spread()[$keys() in $vdcgroups.*.vdcs.id].(
              $id := $keys()[0];
              $name := $.*.name;
              $vdcgroup := $vdcgroups.$spread()[$id in *.vdcs.id];
              $vdc := $;
              $org := $orgs.$spread()[$keys() = $vdc.*.org_id];
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $orgname := $.*.org_name;
              $vdcgroup.$spread().(
                {
                    'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $.*.name & '/' & $name,
                    'link': 'entities/' & $vdcs_entity & '/card?id=' & $id
                }
              )
          );
          $dynamic := $append($dynamic_vdc, $dynamic_vdcgroup); 
          $append($static, $dynamic)
      )