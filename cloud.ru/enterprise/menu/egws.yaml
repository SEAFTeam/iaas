entities:
  seaf.ta.reverse.cloud_ru.enterprise.egws:
    menu: >
      (
          $domain := '';
          $egws_entity_matcher := /.*\.enterprise\.egws$/;
          $orgs_entity_matcher := /.*\.enterprise\.orgs$/;
          $vdcs_entity_matcher := /.*\.enterprise\.vdcs$/;
          $vdcgroups_entity_matcher := /.*\.enterprise\.vdcgroups$/;
          $dcs_entity_matcher := /.*\.services\.dc$/;
          $dcs := $$.$spread()[$dcs_entity_matcher($keys())].*;
          $vdcgroups := $$.$spread()[$vdcgroups_entity_matcher($keys())].*;
          $egws_entity := $$.entities.$spread()[$egws_entity_matcher($keys())].$keys()[0];
          $egws := $$.$spread()[$egws_entity_matcher($keys())].*;
          $vdcs := $$.$spread()[$vdcs_entity_matcher($keys())].*;
          $orgs := $$.$spread()[$orgs_entity_matcher($keys())].*;
          $config := entities."seaf.ta.reverse.general".config;
          $static := [
              {
                  'location': $config.arch_menu & '/Cloud.ru/Enterprise/Реестры/Шлюзы (Edge Gateway)',
                  'link': 'entities/' & $egws_entity & '/list?domain=' & $domain
              }
          ];
          $dynamic_vdc := $egws.$spread()[*.vdcgroup_id = null].(
              $id := $keys()[0];
              $name := $.*.name;
              $vdcname := $.*.vdc_name;
              $vdc_id := $.*.vdc_id;
              $org_id := $.*.org_id;
              $vdcgroup := $vdcgroups.$spread()[$vdc_id in *.vdcs.id];
              $vdcgroupname := $vdcgroup.*.name;
              $org := $orgs.$spread()[$keys() = $org_id];
              $orgname := $org.*.name;
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $exists($vdcgroup) ? 
                  [
                      {
                          'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgroupname & '/Edge Gateways/' & $name,
                          'link': 'entities/' & $egws_entity & '/schema_vdcgroup?id=' & $id
                      },
                      {
                          'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgroupname & '/Edge Gateways',
                          'link': 'entities/' & $egws_entity & '/list?vdcgroup_id=' & $vdcgroup.*.id
                      }     
                  ]
                  :
                    [
                        {
                            'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcname & '/Edge Gateways/' & $name,
                            'link': 'entities/' & $egws_entity & '/schema?id=' & $id
                        },
                        {
                            'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcname & '/Edge Gateways',
                            'link': 'entities/' & $egws_entity & '/list?vdc_id=' & $vdc_id
                        }     
                    ]
          );
          $dynamic_vdcgroup := $egws.$spread()[*.vdc_id = null].(
              $id := $keys()[0];
              $name := $.*.name;
              $vdcgroupname := $.*.vdcgroup_name;
              $vdcgroup_id := $.*.vdcgroup_id;
              $org_id := $.*.org_id;
              $org := $orgs.$spread()[$keys() = $org_id];
              $orgname := $org.*.name;
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              [
                  {
                      'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgroupname & '/Edge Gateways/' & $name,
                      'link': 'entities/' & $egws_entity & '/schema_vdcgroup?id=' & $id
                  },
                  {
                      'location': $config.arch_menu & '/Провайдеры/' & $dc.*.title & '/Enterprise/Организации/' & $orgname & '/' & $vdcgroupname & '/Edge Gateways',
                      'link': 'entities/' & $egws_entity & '/list?vdcgroup_id=' & $vdcgroup_id
                  }     
              ]
          );
          $dynamic := $append($dynamic_vdc, $dynamic_vdcgroup);
          $append($static, $distinct($dynamic))
      )