entities:
  seaf.ta.reverse.vmwarecloud.orgnets:
    menu: >
      (
          $domain := '';
          $domain_matcher := $eval('/^' & $domain & '.*/');
      
          $vdcs_entity := "seaf.ta.reverse.general".config.entities.vdcs.model;
          $dcs_entity := "seaf.ta.reverse.general".config.entities.dc.model;
          $orgs_entity := "seaf.ta.reverse.general".config.entities.orgs.model;
          $vdcgroups_entity := "seaf.ta.reverse.general".config.entities.vdcgroups.model;
          $orgnets_entity := "seaf.ta.reverse.general".config.entities.orgnets.model;
          $orgnets_view := "seaf.ta.reverse.general".config.entities.orgnets.view;
      
          $dcs := $lookup($, $dcs_entity)[$domain_matcher($keys())];
          $orgnets := $lookup($, $orgnets_entity)[$domain_matcher($keys())];
          $vdcs := $lookup($, $vdcs_entity)[$domain_matcher($keys())];
          $vdcgroups := $lookup($, $vdcgroups_entity)[$domain_matcher($keys())];
          $orgs := $lookup($, $orgs_entity)[$domain_matcher($keys())];
      
          $config := "seaf.ta.reverse.general".config;
          $lists := [$orgs.$spread().(
              $dcid := $.*.DC;
              $dcname := $dcs.$spread()[$keys() = $dcid].*.title;
              {
                  'location': $config.arch_menu & '/' & $dcname & '/Облако VMware/Реестры/Сети',
                  'link': 'entities/' & $orgnets_view & '/list?domain=' & $domain
              }
          )];
          /*$static := [
              {
                  'location': $config.arch_menu &  '/Cloud.ru/Облако VMware/Реестры/Сети',
                  'link': 'entities/' & $orgnets_view & '/list?domain=' & $domain
              }
          ];*/
          $dynamic_vdc := $orgnets.$spread()[$not($keys() in $vdcgroups.*.networks.id)].(
              $id := $keys()[0];
              $name := $replace($.*.name, '/', ' ');
              $org_id := $.*.org_id;
              $org := $orgs.$spread()[$keys() = $org_id];
              $orgname := $org.*.name;
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $vdc_id := $.*.vdc_id;
              $vdcname := $vdcs.$spread()[$keys() = $vdc_id].*.name;
              $vdc_id in $vdcgroups.*.vdcs.id ? (
                  $vdcgroups.$spread()[$vdc_id in *.vdcs.id].(
                      $vdcgname := $.*.name;
                      [
                          {
                            'location': $config.arch_menu & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgname & '/' & $vdcgname & '/' & $vdcname & '/Сети организации/' & $name,
                            'link': 'entities/' & $orgnets_view & '/schema?id=' & $id
                          },
                          {
                            'location': $config.arch_menu & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgname & '/' & $vdcgname & '/' & $vdcname & '/Сети организации',
                            'link': 'entities/' & $orgnets_view & '/list?vdc_id=' & $vdc_id
                          }
                      ]
                  )
              ) : (
                  [
                      {
                        'location': $config.arch_menu & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgname & '/' & $vdcname & '/Сети организации/' & $name,
                        'link': 'entities/' & $orgnets_view & '/schema?id=' & $id
                      },
                      {
                        'location': $config.arch_menu & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgname & '/' & $vdcname & '/Сети организации',
                        'link': 'entities/' & $orgnets_view & '/list?vdc_id=' & $vdc_id
                      }
                  ]
              )
          );
          $dynamic_vdcgroup := $orgnets.$spread()[$keys() in $vdcgroups.*.networks.id].(
              $id := $keys()[0];
              $name := $replace($.*.name, '/', ' ');
              $org_id := $.*.org_id;
              $org := $orgs.$spread()[$keys() = $org_id];
              $orgname := $org.*.name;
              $dc := $dcs.$spread()[$keys() = $org.*.DC];
              $vdcgroup_id := $.*.vdcgroup_id;
              $vdcgroupname := $vdcgroups.$spread()[$keys() = $vdcgroup_id].*.name;
              [
                  {
                    'location': $config.arch_menu & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgname & '/' & $vdcgroupname & '/Сети организации/' & $name,
                    'link': 'entities/' & $orgnets_view & '/schema_vdcgroup?id=' & $id
                  },
                  {
                    'location': $config.arch_menu & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgname & '/' & $vdcgroupname & '/Сети организации',
                    'link': 'entities/' & $orgnets_view & '/list?vdcgroup_id=' & $vdcgroup_id
                  }
              ]
          );
          $dynamic := $append($dynamic_vdc, $dynamic_vdcgroup);
          $append($lists, $distinct($dynamic))
      )