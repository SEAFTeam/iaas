entities:
  seaf.ta.reverse.vmwarecloud.vdcs:
    menu: >
      (
          $vdcs_entity := "seaf.ta.reverse.general".config.entities.vmwarecloud.vdcs.model;
          $vdcs_view := "seaf.ta.reverse.general".config.entities.vmwarecloud.vdcs.view;
          $dcs_entity := "seaf.ta.reverse.general".config.entities.dc.model;
          $orgs_entity := "seaf.ta.reverse.general".config.entities.vmwarecloud.orgs.model;
          $vdcgroups_entity := "seaf.ta.reverse.general".config.entities.vmwarecloud.vdcgroups.model;
          
          $vdcs_menu_generator := function($prefix, $domain){(
              $domain_matcher := $eval('/^' & $domain & '.*/');
      
              $dcs := $lookup($, $dcs_entity)~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
              $vdcs := $lookup($, $vdcs_entity)~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
              $vdcgroups := $lookup($, $vdcgroups_entity)~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
              $orgs := $lookup($, $orgs_entity)~> $each(function($v, $k){$domain_matcher($k) ? {$k:$v}});
              
              $lists := [$orgs.$spread().(
                  $dcid := $.*.dc;
                  $dctitle := $dcs.$spread()[$keys() = $dcid].*.title;
                  {
                      'domain': $domain,
                      'location': $prefix & '/' & $dctitle & '/Облако VMware/Реестры/VDC',
                      'link': 'entities/' & $vdcs_view & '/list?domain=' & $domain
                  }
              )];
              /*$static := [
                  {
                      'location': $config.arch_menu & '/Cloud.ru/Облако VMware/Реестры/VDC',
                      'link': 'entities/' & $vdcs_view & '/list?domain=' & $domain
                  }
              ];*/
              $dynamic_vdc := $vdcs.$spread()[$not($keys() in $vdcgroups.*.vdcs.id)].(
                  $id := $keys()[0];
                  $title := $.*.title;
                  $vdc := $;
                  $org := $orgs.$spread()[$keys() = $vdc.*.org];
                  $dc := $dcs.$spread()[$keys() = $org.*.dc];
                  $orgtitle := $.*.org_title;
                  {
                      'domain': $domain,
                      'location': $prefix & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgtitle & '/' & $title,
                      'link': 'entities/' & $vdcs_view & '/card?id=' & $id
                  }
              );
              $dynamic_vdcgroup := $vdcs.$spread()[$keys() in $vdcgroups.*.vdcs.id].(
                  $id := $keys()[0];
                  $title := $.*.title;
                  $vdcgroup := $vdcgroups.$spread()[$id in *.vdcs.id];
                  $vdc := $;
                  $org := $orgs.$spread()[$keys() = $vdc.*.org];
                  $dc := $dcs.$spread()[$keys() = $org.*.dc];
                  $orgtitle := $.*.org_title;
                  $vdcgroup.$spread().(
                      {
                          'domain': $domain,
                          'location': $prefix & '/' & $dc.*.title & '/Облако VMware/Организации/' & $orgtitle & '/' & $.*.title & '/' & $title,
                          'link': 'entities/' & $vdcs_view & '/card?id=' & $id
                      }
                  )
              );
              $dynamic := $append($dynamic_vdc, $dynamic_vdcgroup); 
              $append($lists, $dynamic);
          )};
          sber.kadzo ? (
              sber.kadzo.enterprise_mode ?
              sber.domains.$spread().( 
                  $domain := $keys(); 
                  $title := $lookup($$.companies, $domain) ? $lookup($$.companies, $domain).title : $.*.title;
                  $prefix := "ДЗО/" & $title & "/Реверс";
                  $vdcs_menu_generator($prefix, $domain);
              ) : (
                  $not($exists(sber.kadzo.enterprise_mode)) and (sber.kadzo.menu.architecture.enabled = true or $not($exists(sber.kadzo.menu.architecture.enabled))) ? (
                      $menu_root := ($exists(sber.kadzo.menu.architecture.roots) and $count(sber.kadzo.menu.architecture.roots) > 0 and $not(sber.kadzo.menu.architecture.roots = null))  ? sber.kadzo.menu.architecture.roots :
                          $exists(sber.kadzo.menu_root) ? sber.kadzo.menu_root : "Документы/Сбер/КА ДЗО/2023";
                      $domain := sber.domain;
                      $prefix := $menu_root & "/Реверс";
                      $vdcs_menu_generator($prefix, $domain);
                  )
              ) 
          ) : (
              $config := "seaf.ta.reverse.general".config;
              $prefix := $config.arch_menu;
              $domain := "";
              $vdcs_menu_generator($prefix, $domain);
          );
      )